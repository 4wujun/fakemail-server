#!groovy

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
}

plugins {
	id 'org.liquibase.gradle' version '1.2.4'
	id 'org.springframework.boot' version '1.5.7.RELEASE'
	id 'com.github.ben-manes.versions' version '0.15.0'
	id 'org.sonarqube' version '2.5'
	id 'at.comm_unity.gradle.plugins.jpamodelgen' version '1.1.4'
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'
//apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'jacoco'

group = 'org.legurun.test'
version = '0.2.0-SNAPSHOT'
description = """Fakemail server aims to act as a "blackhole" mail server.
	Each mail send to Fakemail is stored into database and never forwarded to legitimate recipients.
	Those mails can be displayed on a web interface.
	The main use is for testing software which sends mail, to forbid sending to real users."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext['spring.version'] = '4.3.11.RELEASE'
ext['hibernate.version'] = '5.2.11.Final'
ext['tomcat.version'] = '8.5.20'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.2'
}

configurations {
	providedCompile
}

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-cache'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.boot:spring-boot-starter-mail'
	compile 'org.springframework.boot:spring-boot-starter-validation'
	compile "org.liquibase:liquibase-core:3.5.3"
	compile "org.liquibase.ext:liquibase-hibernate5:3.6"
	compile "org.liquibase.ext:liquibase-javalogger:3.0"
	compile "net.bull.javamelody:javamelody-spring-boot-starter:1.69.0"
	compile "org.subethamail:subethasmtp:3.1.7"
	compile "javax.cache:cache-api:1.0.0"
	compile "org.ehcache:ehcache:3.4.0"
	compile "org.hibernate:hibernate-jcache:+"
	compile "org.hibernate:hibernate-jpamodelgen"
	compile "org.apache.commons:commons-dbcp2"
	runtime "org.apache.tomcat.embed:tomcat-embed-jasper"
	compile "javax.servlet:jstl"
	compile "taglibs:standard:1.1.2"
	compile "org.springframework.boot:spring-boot-devtools"
	compile "org.webjars:bootstrap:3.3.7-1"
	compile "org.webjars:jquery:3.2.1"
	compile "org.webjars:bootstrap-table:1.9.1-1"
	compile "org.webjars:jquery-dateFormat:1.0.2-1"
	compile "org.webjars:bootstrap-notify:3.1.3-1"
	compile "org.webjars:bootstrap-datetimepicker:2.4.2"
	compile "org.webjars:webjars-locator:0.32-1"
	runtime "com.h2database:h2:1.4.196"
	runtime "org.mariadb.jdbc:mariadb-java-client:2.1.1"
	compile "com.google.code.findbugs:annotations:3.0.1u2"
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile "org.mockito:mockito-core:2.10.0"
}

jpaModelgen {
	library "org.hibernate:hibernate-jpamodelgen:5.2.11.Final"
}
compileJava.options.compilerArgs += [ "-proc:none" ]

springBoot {
	buildInfo()
}

bootRun {
	addResources = true
}

/*
task('runH2', type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
	doFirst {
		System.setProperty('externalConfigurationLocation', "${project.rootDir}/src/main/config/application-h2.properties")
	}
}

task('runMysql', type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
	doFirst {
		System.setProperty('externalConfigurationLocation', "${project.rootDir}/src/main/config/application-mysql.properties")
	}
}

tomcat {
	httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
	ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}
*/

checkstyle {
	configDir = file("${project.rootDir}/src/config")
	sourceSets = [project.sourceSets.main]
	
}
tasks.withType(Checkstyle) {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

pmd {
	ruleSetFiles = files("${project.rootDir}/src/config/pmd.xml")
	ignoreFailures = true
	sourceSets = [project.sourceSets.main]
}
tasks.withType(Pmd) {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}

findbugs {
	excludeFilter = file("${project.rootDir}/src/config/findbugsExcludeFilter.xml")
	ignoreFailures = true
	sourceSets = [project.sourceSets.main]
}
tasks.withType(FindBugs) {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}

jdepend {
	sourceSets = [project.sourceSets.main]
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = false
	}
}
check.dependsOn jacocoTestReport

war {
	baseName = 'fakemailserver'
	manifest {
		attributes('Implementation-Title': project.name,
				'Implementation-Version': version,
				'Built-By': System.getProperty('user.name'),
				'Built-Date': new Date(),
				'Built-JDK': System.getProperty('java.version'),
				'Built-Gradle': gradle.gradleVersion,
				'Class-Path': configurations.runtime.files.collect { it.name }.join(' ')
		)
	}
}
/*
liquibase {
	activities {
		main {
			changeLogFile = "${projectDir}/src/main/resources/liquibase/changelog-master.xml"
			url 'jdbc:mysql://localhost:3306/fakemail_ref?nullNamePatternMatchesAll=true'
			username
			password
			//src/main/resources/liquibase/liquibase.properties
		}
	}
}
*/

sonarqube {
	properties {
		property 'sonar.projectKey', 'org.legurun.fakemailserver'
		property 'sonar.host.url', 'https://sonarcloud.io/'
		property 'sonar.organization', 'patlenain-github'
	}
}

eclipse {
	project {
		natures 'net.sf.eclipsecs.core.CheckstyleBuilder'
		natures 'ch.acanda.eclipse.pmd.builder.PMDBuilder'
		natures 'org.eclipse.buildship.core.gradleprojectnature'
		buildCommand 'net.sf.eclipsecs.core.CheckstyleNature'
		buildCommand 'ch.acanda.eclipse.pmd.builder.PMDNature'
		buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
	}
	classpath {
		downloadJavadoc = true
		downloadSources = true
	}
}

