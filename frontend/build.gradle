#!groovy

plugins {
	id 'com.moowork.node' version '1.2.0'
}

apply plugin: 'eclipse'

node {
	version = '8.11.4'
	download = true
}

description = "Angular 6 frontend"

task clean(type: Delete) {
	group = 'build'
	description = 'Clean-up generated files'
	delete "${projectDir}/tests"
}

task prepareBuild(type: Delete) {
	group = 'build'
	description = 'Prepare distribution directory for building'
	delete fileTree(dir: "${rootDir}/server/src/main/resources/static/",
		 excludes: [ '.gitignore', '.keep' ])
}

task buildClientDev(type: NpmTask, dependsOn: ['npmInstall','prepareBuild']) {
	group = 'build'
	description = 'Compile client side folder for development'
	args = ['run','buildDevClient']
}

task buildClientWatch(type: NpmTask, dependsOn: ['npmInstall','prepareBuild']) {
	group = 'application'
	description = 'Build and watches the client side assets for rebuilding'
	args = ['run','buildWatch']
}

task buildClient(type: NpmTask, dependsOn: ['npmInstall','prepareBuild']) {
	group = 'build'
	description = 'Compile client side folder for production'
	args = ['run','build']
}

task testNpm(type: NpmTask, dependsOn: 'npmInstall') {
	group = 'verification'
	description = 'Launch unit tests'
	args = ['run', 'test']
}

task testCoverage(type: NpmTask, dependsOn: 'npmInstall') {
	group = 'verification'
	description = 'Launch coverage analysis'
	args = ['run', 'testCoverage']
}

eclipse {
	project {
		natures 'org.eclipse.buildship.core.gradleprojectnature'
		natures 'org.eclipse.wst.common.project.facet.core.nature'
		buildCommand 'org.eclipse.wst.common.project.facet.core.builder'
		buildCommand 'org.eclipse.wst.validation.validationbuilder'
	}
}

sonarqube {
	properties {
		property 'sonar.sources', 'src'
		property 'sonar.exclusions', 'src/test.ts,**/*.spec.ts'
		property 'sonar.tests', 'src'
		property 'sonar.test.inclusions', 'src/test.ts,**/*.spec.ts'
		property 'sonar.javascript.lcov.reportPaths', 'reports/lcov.info'
		property 'sonar.typescript.lcov.reportPaths', 'reports/lcov.info'
	}
}

